#!/usr/bin/perl -w
use strict;

use FindBin;
my $TOP;
BEGIN { $TOP = "$FindBin::RealBin/../.." }
use lib "$TOP/code";

use Getopt::Long;
use Loom::File;
use Loom::Int128;

my @bad_types;
my $num_types = 0;
my $verbose = 0;
my $show_subtotal = 0;
my $data_dir;
my $type_zero = "0" x 32;

main();

sub main
	{
	get_options();
	audit_grid();

	exit(0);
	}

sub get_options
	{
	my $opt = {};
	my $ok = GetOptions($opt,"l","s","h");

	if (!$ok || $opt->{h})
		{
		my $prog_name = $0;
		$prog_name =~ s#.*/##;

	print STDERR <<EOM;
Usage: $prog_name [-l | -s]

Audit the Loom grid.
  $prog_name      # Check, but don't show detail.
  $prog_name -l   # List grid values.
  $prog_name -s   # Show subtotals.
EOM
		exit(2);
		}

	$verbose = $opt->{l} || $opt->{s};
	$show_subtotal = $opt->{s};

	return;
	}

sub audit_grid
	{
	$data_dir = Loom::File->new("$TOP/data/app");
	$data_dir->restrict;

	print "-- balances by type\n" if $verbose;

	my $place = $data_dir->child("grid");
	for my $leg ($place->names)
		{
		my $place = $place->child($leg);
		for my $leg ($place->names)
			{
			my $place = $place->child($leg);
			for my $type ($place->names)
				{
				check_type($place,$type);
				}
			}
		}

	print "\n" if $verbose;

	if (@bad_types)
		{
		print "--- FAIL: Something is wrong with the following types:\n";
		for my $type (@bad_types)
			{
			print "  $type\n";
			}
		exit(3);
		}

	print "--- SUCCESS:  All $num_types types look good.\n";

	return;
	}

sub check_type
	{
	my $place = shift;
	my $type = shift;

	my $issuer;
	{
	my $file = $place->child("$type/I");
	$file->read;
	$issuer = $file->get;
	$issuer = "" if !defined $issuer;
	}
	my $dsp_issuer = substr($issuer,0,8);

	print "\n" if $verbose;
	if ($verbose)
		{
		print "type $type issuer $dsp_issuer\n";
		}
	$num_types++;

	my $type_total = Loom::Int128->from_dec("0");

	$place = $place->child("$type/V");
	for my $leg ($place->names)
		{
		my $place = $place->child($leg);
		for my $leg ($place->names)
			{
			my $place = $place->child($leg);
			for my $hash ($place->names)
				{
				my $place = $place->child($hash);
				$place->read;
				my $val = $place->get;

				$type_total->add(Loom::Int128->from_dec($val));
				my $q_total = $type_total->to_dec;

				my $q_hash = substr($hash,0,8);
				if ($verbose)
					{
					if ($show_subtotal)
						{
						printf "  %-8s %20s %20s\n", $q_hash, $val, $q_total;
						}
					else
						{
						printf "  %-8s %20s\n", $q_hash, $val;
						}
					}
				}
			}
		}

	my $q_total = $type_total->to_dec;

	if ($verbose)
		{
		if ($show_subtotal)
			{
			printf "  %-8s %20s %20s\n", "TOTAL", "", $q_total;
			}
		else
			{
			printf "  %-8s %20s\n", "TOTAL", $q_total;
			}
		}

	if ($type eq $type_zero)
		{
		print "  (Any total is acceptable for type zero.)\n" if $verbose;
		}
	else
		{
		if ($q_total eq "-1")
			{
			# The type totals to -1 as expected.  Let's make sure the
			# issuer location has been bought.
			if ($issuer eq "")
				{
				# ERROR: Issuer location has not been bought.
				push @bad_types, $type;
				}
			}
		elsif ($q_total eq "0")
			{
			# The type totals to 0.  In this case we expect that nobody
			# has yet bought the issuer location (which is why the -1
			# didn't appear during the key scan).

			if ($issuer ne "")
				{
				# ERROR: Issuer location has been bought, but we total
				# to 0.
				push @bad_types, $type;
				}
			}
		else
			{
			push @bad_types, $type;
			}
		}
	}
